def versionMajor = 1
def versionMinor = 0
def versionPatch = 0
def versionBuild = 0 // bump for dogfood builds, public betas, etc.


apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt' // Use experimental kapt implementation


def gitSha() {
    def p = 'git rev-parse --short HEAD'.execute([], project.rootDir)
    p.waitFor()
    if (p.exitValue() != 0) {
        throw new RuntimeException(p.errorStream.text)
    }

    return p.text.trim()
}
def gitTimestamp() {
    def p = 'git log -n 1 --format=%at'.execute([], rootDir)
    p.waitFor()
    if (p.exitValue() != 0) {
        throw new RuntimeException(p.errorStream.text)
    }

    return p.text.trim()
}
buildscript {
    repositories {
        mavenCentral()
    }
}


android {
    compileSdkVersion versions.compileSdk
    defaultConfig {
        applicationId "com.combustela.combustela"
        minSdkVersion versions.minSdk
        targetSdkVersion versions.targetSdk

        buildConfigField 'String', 'GIT_SHA', "\"${gitSha()}\""
        buildConfigField 'long', 'GIT_TIMESTAMP', "${gitTimestamp()}L"

        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true
    }
    buildTypes {
        debug {
            applicationIdSuffix '.debug'
            resValue("string", "PORT_NUMBER", "8081")
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {

    //Kotlin
    implementation deps.kotlin


    implementation deps.support.appCompat
    implementation deps.support.v4
    implementation deps.support.design
    implementation deps.support.constraint
    implementation deps.support.vector
    implementation deps.support.cardview
    implementation deps.support.grid
    implementation deps.support.customtabs
    implementation deps.support.exif

    //==================== Network ====================

    implementation deps.glide.glide
    kapt deps.glide.compiler

    // Retrofit
    implementation deps.okhttp.logger
    implementation deps.retrofit.core
    implementation deps.retrofit.moshi
    implementation deps.retrofit.rxjava

    //==================== Structure ====================

    //Rx
    implementation deps.rx.android
    implementation deps.rx.kotlin

    //DI
    kapt deps.dagger.compiler
    implementation deps.dagger.runtime

    //arch
    implementation deps.lifecycle.runtime
    implementation deps.lifecycle.extensions
    implementation deps.lifecycle.reactivestreams
    implementation deps.room.runtime
    kapt deps.lifecycle.compiler

    //Play
    implementation deps.play.maps
    implementation deps.play.mapsutils
    implementation deps.play.location
    implementation deps.play.places
    implementation deps.geocoding
    implementation deps.locationrx
    implementation deps.permissionrx


    implementation deps.firebase.core
    implementation deps.firebase.messaging

    implementation deps.stetho.core

    implementation deps.timber
    debugImplementation deps.debugdb

    //==================== UI ====================
    implementation deps.circleimageview
    implementation deps.photoview
    implementation 'com.googlecode.libphonenumber:libphonenumber:8.9.14'


    androidTestImplementation deps.espresso.core
    testImplementation deps.espresso.contrib
    testImplementation deps.mockito
    androidTestImplementation deps.junit
    testImplementation deps.lifecycle.coretesting



    //==================== Tools ====================
    implementation 'org.jsoup:jsoup:1.10.1'
    implementation 'com.airbnb.android:lottie:2.5.5'
    implementation 'com.facebook.shimmer:shimmer:0.3.0'

    implementation(deps.crashlytics.core) {
        transitive = true
    }


}
